apply plugin: 'com.android.application'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId 'org.secuso.privacyfriendlyfinancemanager'
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 6
        versionName '2.2.0'
    }
    buildFeatures {
        //noinspection DataBindingWithoutKapt
        dataBinding true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        // java-11 to allow to use java stream api
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds true
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation project(':backup-api')

    testImplementation 'junit:junit:4.13.2'

    // for unittests replaces net.danlew:android.joda that causes 'Resource not found: "org/joda/time/tz/data/ZoneInfoMap"'
    // see https://github.com/dlew/joda-time-android/issues/37
    testImplementation 'joda-time:joda-time:2.9.4.2'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'net.danlew:android.joda:2.10.12.2'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    implementation 'net.zetetic:android-database-sqlcipher:4.5.0'
    implementation 'androidx.room:room-runtime:2.4.0'
    annotationProcessor 'androidx.room:room-compiler:2.4.0'

    def work_version = '2.7.1'
    implementation "androidx.work:work-runtime:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    implementation 'com.github.QuadFlask:colorpicker:0.0.15'

    // https://opencsv.sourceforge.net/
    implementation 'com.opencsv:opencsv:5.7.1'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
}
